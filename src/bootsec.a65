;
; BOOT-LOADER for DOS.SYS (FAT16.COM + DUP.COM)
;
; START AT $A912
;
; Program writes bootloader from $A800 into 3 bootsectors of disk D2:
;
; Bootloader identifies DOS.SYS in the directory and reads it as a normal COM file
; DosInit points to $0D00
;
PTR1	= 2
PTR2	= 4
;
SECCNT0	= $C4
DIRCNT	= $C5
;
COPEN	= 3
CCLOSE	= 12
CBPUT	= 11
ICCOM	= $342
ICADRL	= $344
ICADRH	= $345
ICLENL	= $348
ICLENH	= $349
ICAUX1	= $34A
ICAUX2	= $34B
;
CIOV	= $E456
;
COLBAK	= $D01A
WSYNC	= $D40A
VCOUNT	= $D40B

;
; PAGE 3
;
DDEVIC   = $0300
DUNIT    = $0301
DCOMND   = $0302
DSTATS   = $0303
DBUFLO   = $0304
DBUFHI   = $0305
DTIMLO   = $0306
DUNUSE   = $0307
DBYTLO   = $0308
DBYTHI   = $0309
DAUX1    = $030A
DAUX2    = $030B

PTR	= $0
SAVMSC	= $58

SIOV	= $E459
DSKINV	= $E453

EOUTCH	= $F2B0

PORTB	= $D301
NMIEN	= $D40E

DIRSEC	= $169		; until $170
BUFFER	= $0480
SECLEN	= BUFFER+127

DOSINIT	= $D00
;
;
;
;		ORG $AA00
;	
;LOOP1		LDA VCOUNT
;		STA WSYNC
;		STA COLBAK
;		JMP LOOP1
;	
	
	
;	
		.ORG $700
;		ORG $A800
;		JMP LOADER
;	
BOOTTAB:	.byte 0
SECCNT:		.byte 3
LADDR:		.word $0700	;LOAD ADDR
		.word DOSINIT
	
LOADER:		LDA #8
		STA SECCNT0
	
		LDA #<DIRSEC
		STA DAUX1
		LDA #>DIRSEC
		STA DAUX2	
	
FDIR5:		JSR GETSEC
	
		LDX #0
FDIR3:		LDA BUFFER,X
		BEQ ERROR
		CMP #$42
		BNE FDIR1
	
		LDA BUFFER+1,X
		STA PTR2
		LDA BUFFER+2,X
		STA PTR2+1
		LDA BUFFER+3,X
		STA DAUX1
		LDA BUFFER+4,X
		STA DAUX2
	
		LDY #0
FDIR6:		LDA FNAME,Y
		CMP BUFFER+5,X
		BNE FDIR1
		INX
		INY
		CPY #10
		BNE FDIR6
		BEQ READFIL
	
FDIR1:		TXA
		CLC
		AND #$F0
		ADC #16
		TAX
		BPL FDIR3
	
FDIR2:		DEC SECCNT0
		BEQ ERROR
		INC DAUX1
		BNE FDIR5
	
ERROR:		BRK
;	
GETSEC:		LDA #'R'
		STA DCOMND
		LDA #1
		STA DUNIT
		LDA #<BUFFER
		STA DBUFLO
		LDA #>BUFFER
		STA DBUFHI
	
		JSR DSKINV
		BMI ERROR
		RTS
;	
;	
;	
READFIL:	LDA #<BUFFER
		STA PTR
		LDA #>BUFFER
		STA PTR+1
	
		JSR INCY1
	
READCHU:	CPY SECLEN
		BNE READCHU1
		LDA PTR2
		ORA PTR2+1
		BNE READCHU2
		CLC
;		TAY
		RTS
READCHU2:	LDY #124
		JSR INCY
READCHU1:	LDA #$FF
		CMP (PTR),Y
		BNE READFI1
		JSR INCY
		CMP (PTR),Y
		BNE READFI1
		JSR INCY
	
READFI1:	LDA (PTR),Y
		STA TOADR+1
		JSR INCY
		LDA (PTR),Y
		STA TOADR+2
		JSR INCY
		LDA (PTR),Y
		STA PTR1
		JSR INCY
		LDA (PTR),Y
		STA PTR1+1
		JSR INCY
	
READFI4:	LDA (PTR),Y
TOADR:		STA $FFFF
		JSR INCY
	
READFI2:	LDA TOADR+1
		CMP PTR1
		BNE READFI3
		LDA TOADR+2
		CMP PTR1+1
		BNE READFI3
		BEQ READCHU
	
READFI3:	INC TOADR+1
		BNE READFI4
		INC TOADR+2
		BNE READFI4
;	
;	
;	
INCY:		INY
		CPY #125
		BNE INCY9
	
		LDA (PTR),Y
		AND #3
		STA DAUX2
		INY
		LDA (PTR),Y
		STA DAUX1	
	
INCY1:		JSR GETSEC
		SEC
		LDA PTR2
		SBC #1
		STA PTR2
		BCS INCY8
		DEC PTR2+1
INCY8:		LDY #0
INCY9:		RTS
;	
FNAME:		.byte "DOS     SYS"
;	
		.ORG *+$A100
;	
		LDX #1
		STX DAUX1
		DEX
		STX DAUX2
	
		LDA #<$A800
		STA DBUFLO
		LDA #>$A800
		STA DBUFHI
	
		JSR PUTSEC
	
		INC DAUX1
		LDA #128
		STA DBUFLO
	
		JSR PUTSEC
	
		INC DAUX1
		LDA #0
		STA DBUFLO
		INC DBUFHI
	
;		JSR PUTSEC	
;		RTS
	
PUTSEC:		LDA #'P'
		STA DCOMND
		LDA #2
		STA DUNIT
		JSR DSKINV
		BMI ERR
		RTS
ERR:		BRK

;